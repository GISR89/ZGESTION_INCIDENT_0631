managed implementation in class zbp_r_dt_inct_0631 unique;
strict ( 2 );
with draft;

define behavior for Z_r_DT_INCT_0631 alias Incidents
persistent table zdt_inct_0631
draft table zdt_d_inct_0631
etag master LocalLastChangedAt
lock master total etag LastChangedAt
authorization master ( instance )

{
  create;
  update;
  delete;

  association _History { create ( features : instance ); with draft; }

  field ( numbering : managed, readonly ) IncUuid;

  field ( readonly ) IncidentId,
                     Status,
                     ChangedDate,
                     LocalCreatedBy,
                     LocalCreatedAt,
                     LocalLastChangedBy,
                     LocalLastChangedAt,
                     LastChangedAt;

  field ( mandatory ) Title,
                      Description,
                      Priority;

  action ( authorization : update ) ChangeStatus
           parameter ZDD_CHANGE_STATUS_0631 result [1] $self;

  side effects { action ChangeStatus affects $self; }

  draft action Activate optimized;
  draft action Discard;
  draft action Edit;
  draft action Resume;
  draft determine action Prepare;

  mapping for zdt_inct_0631
    {
      IncUuid            = inc_uuid;
      IncidentId         = incident_id;
      Title              = title;
      Description        = description;
      Status             = status;
      Priority           = priority;
      CreationDate       = creation_date;
      ChangedDate        = changed_date;
      LocalCreatedBy     = local_created_by;
      LocalCreatedAt     = local_created_at;
      LocalLastChangedBy = local_last_changed_by;
      LocalLastChangedAt = local_last_changed_at;
      LastChangedAt      = last_changed_at;
    }

}

define behavior for ZDDT_INCT_H_0631 //alias <alias_name>
persistent table zdt_inct_h_0631
draft table zdt_d_inct_h_631
lock dependent by _Incidents
authorization dependent by _Incidents

etag master LocalLastChangedAt

{
  update;
  delete;

  association _Incidents { with draft; }

  field ( numbering : managed, readonly ) HisUuid;

    field ( readonly ) IncUUID,
                       LocalCreatedBy,
                       LocalCreatedAt,
                       LocalLastChangedBy,
                       LocalLastChangedAt,
                       LastChangedAt;

mapping for zdt_inct_h_0631
{
      HisUUID            = his_uuid;
      IncUUID            = inc_uuid;
      HisID              = his_id;
      PreviousStatus     = previous_status;
      NewStatus          = new_status;
      Text               = text;
      LocalCreatedBy     = local_created_by;
      LocalCreatedAt     = local_created_at;
      LocalLastChangedBy = local_last_changed_by;
      LocalLastChangedAt = local_last_changed_at;
      LastChangedAt      = last_changed_at;
}

}